//created on: 26-Apr-2014
package info.habot.tm470.drools

// Set the Dialect to  'mvel' 
dialect "mvel"

// Import the various objects from the calling Java Class
import info.habot.tm470.dao.pojo.FusedSensorData
import info.habot.tm470.dao.pojo.NetworkLink
import info.habot.tm470.dao.pojo.NetworkNode
import info.habot.tm470.dao.pojo.StrategicEvent

//global variables
global Integer NORMAL_THRESHOLD
global Integer INFORMATION_THRESHOLD
global Integer DIVERSION_THRESHOLD

rule "StrategicEvent has a type of Accident (HABOT-01-01)"
	salience 20
    when
        $se : StrategicEvent( event_type == $se.EVENT_TYPE_ACCIDENT, type_suitable == false, severity_suitable == false )
    then
        modify( $se ) {
			setType_suitable (true)
		};
end


rule "StrategicEvent is not affecting traffic conditions (HABOT-01-02)"
	salience 10
    when
    	// NORMAL_THRESHOLD is 60. This is normal traffic conditions
        $se : StrategicEvent( type_suitable == true, severity_suitable == false, capacity_reduction < NORMAL_THRESHOLD, event_sub_type == null )
    then
        modify( $se ) {
			setEvent_sub_type ($se.SUBTYPE_NORMAL)
		};
end

rule "StrategicEvent location is suitable to determine alternative routes (HABOT-01-03)" 
	salience 10
    when
    	link : NetworkLink( )
        $se : StrategicEvent( link_id == link.linkId, location_suitable == false, severity_suitable == false )
    then
    	modify( $se ) {
        	setLocation_suitable( true )
        };
end

rule "StrategicEvent is an Information event (HABOT-01-04)"  
	salience 8
    when
    	// INFORMATION_THRESHOLD is 40. Anything above this is considered an information event
        $se : StrategicEvent( type_suitable == true, severity_suitable == false, capacity_reduction > INFORMATION_THRESHOLD )
    then
        modify( $se ) {
			setSeverity_suitable( true ),
			setEvent_sub_type ($se.SUBTYPE_INFORMATION)
		};
end

rule "StrategicEvent is an Diversion event (HABOT-01-05)"  
	salience 8
    when
    	// DIVERSION_THRESHOLD is 60. Anything above this should trigger an alternative route
        $se : StrategicEvent( type_suitable == true, severity_suitable == true, capacity_reduction > DIVERSION_THRESHOLD)
    then
         modify( $se ) {
			setEvent_sub_type ($se.SUBTYPE_DIVERSION)
		};
end

rule "StrategicEvent alternative route considered (HABOT-01-06)"  salience -50
    when
        $se : StrategicEvent( type_suitable == true, location_suitable == true, severity_suitable == true, consider_alternative_route == false )
    then
        modify( $se ) {
			setConsider_alternative_route( true )
		};
end